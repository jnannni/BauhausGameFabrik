title: SecondScene
tags:
---
//variables for inside the Yarn
<<fadeIn>>
<<set $hasClueD to false>>
<<set $openedInventory to false>>

//variables for using in Unity
<<set $openInventory to false>>
<<set $triggerback to false>>
<<set $triggerAnimation to false>>
<<fadeOut>>
Magnolia: (What has just happened?)
Magnolia: (My head feels heavy.)
Magnolia: (Did I pass out?)
 -> Get up.
  <<set $triggerAnimation to true>>
  //There should be an animation where the character gets up from the floor.

 <<wait 4>>
 Magnolia: (I never had a dream like this before.)
 Magnolia: (Actually I don't remember any of my dreams.)
 Magnolia: (Even as a child...)
 <<wait 2>>
 Magnolia: (My head REALLY feels fuzzy. I can't concentrate on my memories at all.)
 <<wait 2>>
 Magnolia: (How does one get out of a dream?)
 //new objective: get out of the dream.
===

title: Reflection
---
-> Look into the water.
   Magnolia: (I can't see my face very well.)
   Magnolia: (Have I been this tired?)
       -> Stir the water.
           Magnolia: (It looks like one of those instagram filters.)
             -> Drop a pebble inside.
                Magnolia: (Wait. Did that rock just turn into a key?)
                Magnolia: (But I can't get it. It sunk.)
                   <<set $hasClueD to true>>
       -> Stop looking.
===

title: TheGolem
---
<<if visited_count("TheGolem") > 0>>
  Magnolia: (I don't know what to do with this guy.)
  <<jump TheGolem_Questions>>
<<else>>
  Magnolia: (I can't pass here.)
  Magnolia: (There's no way I can move such a big pile of rocks.)
  <<wait 1>>
  Magnolia: (Wait. Is that a face there?)
   <<jump TheGolem_Questions>>
<<endif>>
===
title: TheGolem_Questions
---
<<if not $openedInventory>>
  -> Inspect.
    Magnolia: (It is a face!)
    Magnolia: (He seems sad.)
    Magnolia: (Me and you both, buddy.)    
  -> Check your pocket. <<if $hasClueD>>
      //code for opening inventory.
      <<set $openInventory to true>>
      <<set $openedInventory to true>>
  -> Leave it be.
<<else>>
 Magnolia: (Should I try to use that stone?)
  -> Put the stone back.
  //triggers back to the Waking World
   <<set $triggerback to true>>
   <<fadeIn>>
   <<fadeOut>>
  -> Leave it be.
<<endif>>
===